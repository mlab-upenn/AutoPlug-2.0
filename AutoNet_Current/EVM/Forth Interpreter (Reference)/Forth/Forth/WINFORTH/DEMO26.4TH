\ DEMO26.4TH -- Illustrates "polling" app which draws in its
\ window in the main loop rather than in a message handler.
\ Copyright (C) 1992-1993 LMI

WCB MAINWIN    \ window control block for frame window

\ arbitrary identifiers for menu items
100 CONSTANT IDM_FILEEXIT
101 CONSTANT IDM_FILEABOUT

\ menu for the frame window
MENU APPMENU 
  " &File" POPUP 
          " E&xit"     IDM_FILEEXIT   MF_ENABLED   MENUITEM 
          " "          0              MF_SEPARATOR MENUITEM 
          " A&bout..." IDM_FILEABOUT  MF_ENABLED   MENUITEM 
          END-POPUP 
END-MENU 

100 EQU ID_PUSHB  \ control ID for OK pushbutton in About Box

  " About DEMO26..."   
  16 16 128 70 DS_MODALFRAME WS_CAPTION D+ WS_POPUP D+
DIALOG  ABOUTDLG
  " FORTHICON"              12 16          -1        ICON
  " DEMO26"                 32 8    96 12  -1        CTEXT
  " Sample WinForth App"    32 20   96 12  -1        CTEXT
  " (C) 1993 LMI"           32 32   96 12  -1        CTEXT
  " OK"                     44 50   40 16  ID_PUSHB  DEFPUSHBUTTON
END-DIALOG

VARIABLE SEED

: (RAND) SEED @ 259 * 3 + 32767 AND DUP SEED ! ;
: RANDOM    ( n1 -- n2 )  (RAND) 32767 */ ;
: RXY ( --- x y ) XMAX 1+ RANDOM  YMAX 1+ RANDOM ;

\ --- colorref ; return random nonzero colorref suitable for 
\ use with PEN, BRUSH, etc.
: RCOLOR   255 RANDOM  255 RANDOM  255 RANDOM  RGB  ;

( --- flag ; this is the callback handler for the test dialog )
: ABOUTDLGPROC  wMsg WM_INITDIALOG =
                IF    TRUE EXIT
                THEN  wMsg WM_COMMAND =
                IF    wParam ID_PUSHB =
                      IF   0 CLOSEDLG THEN
                THEN  FALSE ;

\ msg --- ; send message to frame window
: MSG>MAINWIN  MAINWIN WCB>HWND SWAP 0 0. SendMessage 2DROP ;

\ ^str --- ; display error message and OK button in message box
: ERRORBOX  " DEMO26 Error"  \ messagebox title
            [ MB_ICONHAND MB_OK OR ] LITERAL \ icon, button
            MSGBOX DROP ;

\ --- ; draw one random circle if window is not iconized
: DRAWCIRCLE  MAINWIN WSELECT
              hWnd IsIconic 0=
              IF   RCOLOR PEN   3 PENWIDTH  
                   RXY RXY CIRCLE
              THEN MAINWIN WUNSELECT ;

\ --- This routine handles WM_COMMAND messages from the frame window's menu.
: DOCOMMAND wParam CASE
            IDM_FILEEXIT   OF WM_CLOSE MSG>MAINWIN   ENDOF
            IDM_FILEABOUT  OF ABOUTDLG         \ dialog structure
                              ['] ABOUTDLGPROC \ dialog handler
                              RUNDLG DROP            ENDOF
            ENDCASE ;

\ message handler for the frame window
: WNDPROC   wMsg CASE
            WM_COMMAND OF  \ menu command message
                           DOCOMMAND ENDOF
            WM_DESTROY OF  \ kill message loop
                           0 PostQuitMessage ENDOF
            \ default case: pass message to DefWindowProc()
            DEFMSGHANDLER EXIT
            ENDCASE 0 0 ; \ double value to return from message handler

\ "main word" of sample application;
\ creates the frame window and then polls for messages,
\ drawing in the window between polls (not necessarily between
\ individual messages).
: DEMO      ['] WNDPROC MAINWIN SETMSGHANDLER       \ install msg handler
            " DEMO26" MAINWIN WSETNAME              \ set window caption
            " FORTHICON" MAINWIN WLOADICON DROP     \ set icon
            MAINWIN WCREATE 0=                      \ create frame window
            IF   " Can't create frame window!" ERRORBOX 0 RETURN
            THEN APPMENU MAINWIN WSETMENU FALSE =   \ install window menu
            IF   " Can't create menu!" ERRORBOX 0 RETURN
            THEN MAINWIN WSHOW    \ display frame window
            BEGIN  DRAWCIRCLE     \ draw a random circle
                   PEEKWINMSG     \ process message if any waiting
            UNTIL
	    MAINWIN WCLOSE        \ get rid of frame window
            QUIT ( 0 RETURN ) ;   \ exit application

CR
CR .( To run the random circles demo, type:  DEMO <Enter> ) 
CR 

